apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 25
        versionName "$version"
        testApplicationId "net.kjulio.rxpermissions.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            // setting shrinkResources false causes the following error while running androidTests:
            //
            // A problem was found with the configuration of task ':rxpermissions:packageDebugAndroidTest'.
            // > File './RxPermissions/rxpermissions/build/intermediates/res/resources-debug-androidTest-stripped.ap_'
            // specified for property 'resourceFile' does not exist.
            //
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled = true
        }
    }

    // Instrumented tests have to be run on the debug variant as the release variant requires
    // signing the test apk which is useless for library projects.
    testBuildType "debug"

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    lintOptions {
        abortOnError true
        xmlReport true
        htmlReport true
    }
}

dependencies {

    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'io.reactivex.rxjava2:rxjava:2.0.7'

    testCompile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.18'
    testCompile "org.robolectric:robolectric:3.3.1"

    androidTestCompile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'

}

group = 'net.kjulio.RxPermissions'
version = '1.0.0-beta1'

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'RxPermissions'
        name = 'RxPermissions'

        version {
            name = project.version
            desc = 'An Android library to get Android M\'s runtime permissions with a nice and clean rxJava API.'
            released  = new Date()
            vcsTag = project.version
        }

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/julioromano/RxPermissions.git'
        websiteUrl = 'https://github.com/julioromano/RxPermissions'
        publish = true
    }
    configurations = ['archives']
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
