apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 25
        versionName "$version"
        testApplicationId "net.kjulio.rxpermissions.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
        }
        debug {
            minifyEnabled false
            shrinkResources false
            testCoverageEnabled = true
        }
    }

    // Instrumented tests have to be run on the debug variant as the release variant requires
    // signing the test apk which is useless for library projects.
    testBuildType "debug"

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    lintOptions {
        abortOnError true
        xmlReport true
        htmlReport true
    }
}

dependencies {

    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'io.reactivex.rxjava2:rxjava:2.0.7'

    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'

}

group = 'net.kjulio.rxpermissions'
version = '1.0.0'

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'rxpermissions'

        version {
            name = project.version
            desc = 'An Android library to get Android M\'s runtime permissions with a nice and clean rxJava API.'
            released  = new Date()
            vcsTag = project.version
        }

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/julioromano/RxPermissions.git'
        websiteUrl = 'https://github.com/julioromano/RxPermissions'
        publish = true
    }
    configurations = ['archives']
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
